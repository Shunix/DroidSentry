group = 'com.shunix.droidsentry'
version = '0.1.1'

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "0.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def siteUrl = 'https://github.com/Shunix/DroidSentry'
def gitUrl = 'https://github.com/Shunix/DroidSentry.git'

bintray {
    Properties properties = new Properties()
    boolean isLocal = false;
    if (project.rootProject.file('local.properties').exists()) {
        isLocal = true;
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
    }
    user = isLocal ? properties.getProperty("bintray.user") : System.getenv("bintray.user")
    key = isLocal ? properties.getProperty("bintray.apikey") : System.getenv("bintray.apikey")

    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'maven'
        name = project.name
        desc = 'Performance Monitor for Android'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/Shunix/DroidSentry/issues'
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['aar', 'android', 'example']
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'Performance Monitor for Android'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'shunix'
                        name 'Shunix'
                        email 'admin@shunix.com'
                    }
                }
                scm {
                    connection 'https://github.com/Shunix/DroidSentry.git'
                    developerConnection 'https://github.com/Shunix/DroidSentry.git'
                    url siteUrl

                }
            }
        }
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:23.4.0'
    testCompile 'junit:junit:4.12'
}
